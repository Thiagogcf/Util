For each
    Where EmpCod = 1
    defined by EmpRazSocial
Endfor

Header
    print P_EmpRazSocial
    If &ImpVlrNom <> 1
        Print P_Cabecalho
    Else
        Print P_CabNominal
    Endif
End

&ContBol         = 0
&TotGerTitVlrNom = 0
&BolTit.Clear()
&SdtSubGrupo.Clear()

&FFilNom = 'TODAS'
IF &FFilCod <> 99
    For each
        Where FilCod = &FFilCod
        &FFilNom = FilNom
    EndFor
EndIf

&FTraNom = 'TODOS'
IF &FTraCod <> 0
    For each
        Where TraCod = &FTraCod
        &FTraNom = TraNom
    EndFor
EndIf

&BanNom = 'TODOS'
If &FTitBolBanco <> 0
    For each
        Where BanCod = &FTitBolBanco
        &BanNom = BanNom
    EndFor
EndIf

&ConLimNom = 'TODAS'
If &ConLimCod <> 0
    For each
        Where ConLimCod = &ConLimCod
        &ConLimNom = ConLimNom
    EndFor
Else
    If &SdtContaLimite.Count > 0
        &ConLimNom = 'LISTA'
        &C = 0
        &TitConLimCod() = 0
        For &SdtContaLimiteItem in &SdtContaLimite
            &C += 1
            &TitConLimCod(&C) = &SdtContaLimiteItem.ConLimCod
        Endfor
    EndIf
EndIf

&MovNom = 'TODOS'
If &NfOpeMovCod <> 0
    For each
        Where MovCod = &NfOpeMovCod
        &MovNom = MovNom
    EndFor
EndIf

&TitDocNomDesc = '     0 - TODOS'
If &TitDocCod <> 0 and &SdtTipDoc.Count = 0
    For each
        Where TitDocCod = &TitDocCod
        &TitDocNomDesc = &TitDocCod.ToString() +' - '+ TitDocNom
    EndFor
EndIf


&VenNome = 'TODOS'
If &VenCod <> 0
    For each
        Where TraCod = &VenCod
        &VenNome = TraNom
    EndFor
EndIf

Do Case
    Case &TipBoleto = 1     &TipBoletoDes = 'Somente Impressos'
    Case &TipBoleto = 2     &TipBoletoDes = 'Somente Não Impressos'
    Case &TipBoleto = 3     &TipBoletoDes = 'Todos'
EndCase

If &TipBoleto <> 2
    &DesSituacao = 'Situação:'
    Do Case
        Case &Situacao = 0   &SituacaoDes = 'Financeiro'
        Case &Situacao = 1   &SituacaoDes = 'Digitada'
        Case &Situacao = 2   &SituacaoDes = 'Atualizada'
        Case &Situacao = 3   &SituacaoDes = 'NF Listada'
        Case &Situacao = 4   &SituacaoDes = 'Cancelado'
        Case &Situacao = 5   &SituacaoDes = 'O.C. Listada'
        Case &Situacao = 6   &SituacaoDes = 'Ordem atendida'
        Case &Situacao = 7   &SituacaoDes = 'Ordem atualizada'
        Case &Situacao = 8   &SituacaoDes = 'NF-e à Cancelar'
        Case &Situacao = 9   &SituacaoDes = 'Estorno'
        Case &Situacao = 10  &SituacaoDes = 'Todas'
        Case &Situacao = 11  &SituacaoDes = 'NF Atualizada e Listada'
    EndCase
EndIf

Do Case
    Case &TitSituac = '9'   &TitSituacDes = 'Todos'
    Case &TitSituac = 'A'   &TitSituacDes = 'Aberto'
    Case &TitSituac = 'F'   &TitSituacDes = 'Fechado'
    Case &TitSituac = 'E'   &TitSituacDes = 'Fechado sem Estorno'
EndCase

Do Case
    Case &TitNatSoc = 0   &TitNatSocDes = 'Todos'
    Case &TitNatSoc = 1   &TitNatSocDes = 'Associado'
    Case &TitNatSoc = 2   &TitNatSocDes = 'Funcionário'
    Case &TitNatSoc = 3   &TitNatSocDes = 'Não Associado'
EndCase

Do Case
    Case &Ordem = 0     &DesOrdem = 'Banco | Vencto | Nosso Número'
    Case &Ordem = 1     &DesOrdem = 'Alfabética Cliente/Fornecedor'
    Case &Ordem = 2     &DesOrdem = 'Código Cliente/Fornecedor'
    Case &Ordem = 3     &DesOrdem = 'Valor Menor para o Maior'
    Case &Ordem = 4     &DesOrdem = 'Valor Maior para o Menor'
    Case &Ordem = 5     &DesOrdem = 'Banco | Emissão | Nosso Número'
    Case &Ordem = 6     &DesOrdem = 'Vendedor | Banco | Vencto'
    Case &Ordem = 7     &DesOrdem = 'Vendedor | Alfabética Cliente/Fornecedor'
    Case &Ordem = 8     &DesOrdem = 'Filial | Banco | Vencto | Nosso Número'
EndCase

&TipoDoctoVet() = 0
If &SdtTipDoc.Count > 0
    &TitDocNomDesc = ''
    &Tam = &SdtTipDoc.Count
    CSharp      [!&TipoDoctoVet!] = new int[[!&Tam!]];
    For &I = 1 to &SdtTipDoc.Count
        &TipoDoctoVet(&I) = &SdtTipDoc.Item(&I).DocCod
        If &I = 1
            &TitDocNomDesc += &SdtTipDoc.Item(&I).DocCod.ToString()
        Else
            &TitDocNomDesc += ','+Trim(&SdtTipDoc.Item(&I).DocCod.ToString())
        Endif
    EndFor
EndIf

if &DatIniBaixa = CtoD('0') and &DatFinBaixa = CtoD('0')
    For Each
        Order TitFilCod TitParCodConta TitDatDoc When &FFilCod       <> 99
        Order TitParCodConta TitDatDoc           When &FFilCod        = 99
        Where TitFilCod       = &FFilCod         When &FFilCod       <> 99
        Where TitTraCod       = &FTraCod         When &FTraCod       <> 0
        Where TitDatDoc      >= &FTitDatDocIni
        Where TitDatDoc      <= &FTitDatDocFim
        Where TitDatVct      >= &FTitDatVctIni   When &FTitDatVctIni <> CtoD('0')
        Where TitDatVct      <= &FTitDatVctFim   When &FTitDatVctFim <> CtoD('0')
        Where TitConLimCod    = &ConLimCod       When &ConLimCod     <> 0 and &SdtContaLimite.Count = 0
        Where TitConLimCod   in &TitConLimCod()  When &ConLimCod     = 0 and &SdtContaLimite.Count > 0
        Where TitDocCod       = &TitDocCod       When &TitDocCod     <> 0 and &SdtTipDoc.Count = 0
        Where TitDocCod      in &TipoDoctoVet    When &SdtTipDoc.Count > 0
        Where TitParCodConta  = &ParCodConta     When &ParCodConta   <> 99
        where TitSituac       = &TitSituac       When &TitSituac     = 'A' or &TitSituac = 'F'
        where TitSituac       = 'F'              When &TitSituac     = 'E'
        where TitDatEst.IsEmpty()                When &TitSituac     = 'E'
        Where TitCarNumDoc    = &NumCarga        When &NumCarga      <> 0
        Where TitVenCod       = &VenCod          When &VenCod <> 0
        Where TitTraNat = &TitNatSoc             When &TitNatSoc <> 0

        Msg('Carregando boletos... ' + TitNumero.ToString(),status)
        //RGR 2
        PCarSdtBolTit.Call(TitFilCod,TitTraCod,TitDocCod,TitNumero,TitSufixo,TitNumSeq,&Today,&BolTit)

        If &ImpVlrNom = 1 //Imprime valor nominal
            For &BolTitItem in &BolTit
                If TitFilCod = &BolTitItem.TitFilCod and TitTraCod = &BolTitItem.TitTraCod and TitDocCod = &BolTitItem.TitDocCod
                and TitNumero = &BolTitItem.TitNumero and TitSufixo = &BolTitItem.TitSufixo and TitNumSeq = &BolTitItem.TitNumSeq
                    &BolTitItem.TitSituac = TitSituac
                    &BolTitItem.TitVlrNom = TitVlrNom
                EndIf
            Endfor
        EndIf
    EndFor
else
    For Each
        Order TitFilCod TitParCodConta TitDatDoc When &FFilCod       <> 99
        Order TitParCodConta TitDatDoc           When &FFilCod        = 99
        Where TitFilCod       = &FFilCod         When &FFilCod       <> 99
        Where TitTraCod       = &FTraCod         When &FTraCod       <> 0
        Where TitDatDoc      >= &FTitDatDocIni
        Where TitDatDoc      <= &FTitDatDocFim
        Where TitDatVct      >= &FTitDatVctIni   When &FTitDatVctIni <> CtoD('0')
        Where TitDatVct      <= &FTitDatVctFim   When &FTitDatVctFim <> CtoD('0')
        Where TitConLimCod    = &ConLimCod       When &ConLimCod     <> 0 and &SdtContaLimite.Count = 0
        Where TitConLimCod   in &TitConLimCod()  When &ConLimCod     = 0 and &SdtContaLimite.Count > 0
        Where TitDocCod       = &TitDocCod       When &TitDocCod     <> 0 and &SdtTipDoc.Count = 0
        Where TitDocCod      in &TipoDoctoVet    When &SdtTipDoc.Count > 0
        Where TitParCodConta  = &ParCodConta     When &ParCodConta   <> 99
        where TitSituac       = &TitSituac       When &TitSituac     = 'A' or &TitSituac = 'F'
        where TitSituac       = 'F'              When &TitSituac     = 'E'
        where TitDatEst.IsEmpty()                When &TitSituac     = 'E'
        Where TitCarNumDoc    = &NumCarga        When &NumCarga      <> 0
        where BxaDatMov      >= &DatIniBaixa     when &DatIniBaixa   <> CtoD('0')     //data inicial periodo de baixas
        where BxaDatMov      <= &DatFinBaixa     when &DatFinBaixa   <> CtoD('0')     //data final periodo de baixas
        where BxaDatEst      = CtoD('0')                                             //com estorno efetuado
        Where TitVenCod       = &VenCod          When &VenCod <> 0
        Where TitTraNat = &TitNatSoc             When &TitNatSoc <> 0

        Msg('Carregando boletos... ' + TitNumero.ToString(),status)
        //RGR 2
        PCarSdtBolTit.Call(TitFilCod,TitTraCod,TitDocCod,TitNumero,TitSufixo,TitNumSeq,&Today,&BolTit)

        If &ImpVlrNom = 1 //Imprime valor nominal
            For &BolTitItem in &BolTit
                If TitFilCod = &BolTitItem.TitFilCod and TitTraCod = &BolTitItem.TitTraCod and TitDocCod = &BolTitItem.TitDocCod
                and TitNumero = &BolTitItem.TitNumero and TitSufixo = &BolTitItem.TitSufixo and TitNumSeq = &BolTitItem.TitNumSeq
                    &BolTitItem.TitSituac = TitSituac
                    &BolTitItem.TitVlrNom = TitVlrNom
                EndIf
            Endfor
        EndIf
    EndFor
endif

//RGR 3
&BolTit.Sort('TitBolNosNum')
For &BolTitItem in &BolTit
    If &BolTitItem.TitNfNumr <> 0
        &ParcNum = &BolTitItem.TitSufixo.ToNumeric()
        For Each NfFilCod NfDatEmis NfNumero ParcNum
            Where NfFilCod    = &BolTitItem.TitFilCod
            Where NfDatEmis   = &BolTitItem.TitDatDoc
            Where NfNumero    = &BolTitItem.TitNfNumr
            Where ParcNum     = &ParcNum
            Where NFNumCarga  = &NumCarga       When &NumCarga <> 0


            Msg('Lendo documento: '+NfNumero.ToString(),status)

            &BolTitItem.Situacao = NfSituacao
            &BolTitItem.MovCod   = NfOpeMovCod
            If &BolTitItem.TitBolNosNum = 0
                &ConCod = ParConCod
                Do 'LeConta'
                For each
                    Where RatFilCod  = NfFilCod
                    Where RatDatEmis = NfDatEmis
                    Where RatNumero  = NfNumero
                    Where ParcNum    = ParcNum
                    Defined by ParNossoNr

                    Msg('Lendo Nosso Nro.: '+ParNossoNr.ToString(),status)
                    &BolTitItem.TitBolNosNum  = ParNossoNr
                    &BolTitItem.TitBolBanco   = &ConBanCod
                    &BolTitItem.TitBolConta   = &ConCod
                    &BolTitItem.TitBolConSeq  = &ConSeq
                    &BolTitItem.TitBolAgencia = &ConAgeCod
                EndFor
            EndIf
        EndFor
    EndIf
    If &BolTitItem.TitDatEst <> CtoD('0') and &BolTitItem.Situacao <> 4
        &BolTitItem.Situacao = 9
    EndIf
EndFor


Msg('',status)

//RGR 4

&TitVenCod      = 0
&TitVenNom      = ''
&TotVenTitNom   = 0
&ContBolVen     = 0
&VenAux         = 0
Do Case
    Case &Ordem = 0     &BolTit.Sort('TitBolBanco,TitDatVct,TitBolNosNum')
    Case &Ordem = 1     &BolTit.Sort('TitTraNom,TitDatDoc,TitNumero')
    Case &Ordem = 2     &BolTit.Sort('TitTraCod,TitDatDoc,TitNumero')
    Case &Ordem = 3     &BolTit.Sort('TitVlrNom,TitDatDoc,TitNumero')
    Case &Ordem = 4     &BolTit.Sort('[TitVlrNom],TitDatDoc,TitNumero')
    Case &Ordem = 5     &BolTit.Sort('TitBolBanco,TitDatDoc,TitBolNosNum')
    Case &Ordem = 6     &BolTit.Sort('TitVenCod,TitBolBanco,TitDatVct,TitBolNosNum')
    Case &Ordem = 7     &BolTit.Sort('TitVenCod,TitTraNom,TitDatDoc,TitBolNosNum')
    Case &Ordem = 8     &BolTit.Sort('TitFilCod,TitBolBanco,TitDatVct,TitBolNosNum')
EndCase
For &BolTitItem in &BolTit
    If ((&TipBoleto <> 2 and &BolTitItem.TitBolNosNum > 0) or (&TipBoleto <> 1 and &BolTitItem.TitBolNosNum = 0)) and
       ((&BolTitItem.Situacao = &Situacao) or (&BolTitItem.Situacao in (2,3) and &Situacao = 11) or &Situacao = 10) and
       (&BolTitItem.TitBolBanco = &FTitBolBanco or &FTitBolBanco = 0) and
        //RGR 5
       (&BolTitItem.MovCod   = &NfOpeMovCod or &NfOpeMovCod = 0)

//        If (&BolTitItem.TitVenCod <> &TitVenCod and &BolTitItem.TitVenCod > 0 and &VenAux > 0 and &TitVenNom = 'SEM VENDEDOR')
//            &VenAux = 2
//        EndIf
        &TitSit = &BolTitItem.TitSituac
        If &Ordem in (6,7) and (&BolTitItem.TitVenNom <> &TitVenNom and &TitVenNom <> '')
            // Print Total
            &DesTotVendedor = 'Total Vendedor: ' + &TitVenNom.Trim() + ' -->'
            Print P_Tot_Vendedor
            &ContBolVen     = 0
            &TotVenTitNom   = 0
            Eject
        EndIf

        If &Ordem in (6,7) and (&BolTitItem.TitVenNom <> &TitVenNom)
            // Print Vendedor
            Print P_Vendedor
        EndIf


        &TitVenCod    = &BolTitItem.TitVenCod
        &TitVenNom    = &BolTitItem.TitVenNom
        &TitTraCodAux = &BolTitItem.TitTraCod
        Do 'LeTransacionador'
        If &BolTitItem.TitBolNosNum > 0
            &Imp   = 'Sim'
            &ImpSN = ' '
        Else
            &Imp   = 'Não'
            &ImpSN = '*'
        EndIf

        If &BolTitItem.TitBanCod.IsEmpty()
            &ImpBolEnviado.SetEmpty()
        Else
            &ImpBolEnviado = '*'
        EndIf
        &NfSituacaoDes = ''

        Do Case
            Case &BolTitItem.Situacao = 0       &NfSituacaoDes = 'Financeiro'
            Case &BolTitItem.Situacao = 1       &NfSituacaoDes = 'Digitado'
            Case &BolTitItem.Situacao = 2       &NfSituacaoDes = 'Atualizado'
            Case &BolTitItem.Situacao = 3       &NfSituacaoDes = 'NF Listada'
            Case &BolTitItem.Situacao = 4       &NfSituacaoDes = 'Cancelado'
            Case &BolTitItem.Situacao = 5       &NfSituacaoDes = 'O.C. Listada'
            Case &BolTitItem.Situacao = 6       &NfSituacaoDes = 'Ordem atend.'
            Case &BolTitItem.Situacao = 7       &NfSituacaoDes = 'Ordem atual.'
            Case &BolTitItem.Situacao = 8       &NfSituacaoDes = 'NF-e à Canc.'
            Case &BolTitItem.Situacao = 9       &NfSituacaoDes = 'Estorno'
        EndCase

        &DV = ''

        If &BolTitItem.TitBolBanco = 41


            &StrCodAux  =  PadL(TRIM(STR(&BolTitItem.TitBolNosNum)),8,'0')

            //Calcula Digito Verificador
            PCalcDV10Banrisul.Call(&StrCodAux,&DVMod10)

            &StrCodAux  = &StrCodAux.ToString().Trim().PadLeft(8,'0') + &DVMod10.ToString().Trim()

            PCalcDV11Banrisul.Call(&StrCodAux,8,'NN',&DVInvalido,&Mod10,&DVMod11)


            If &DVInvalido = 1
                &DVMod10 = &Mod10
            EndIf

            &DV = &DVMod10.ToString().Trim() + &DVMod11.ToString().Trim()

        endIF

        If &ImpResSubGrupo = 1
            &NfNumero   = &BolTitItem.TitNfNumr
            &NfFilCod   = &BolTitItem.TitFilCod
            &NfDatEmis  = &BolTitItem.TitDatDoc
            &NfForCod   = &BolTitItem.TitTraCod
            Do 'LeNF'

            &Existe      = 0
            For &SdtSubGrupoItem in &SdtSubGrupo
                If &SdtSubGrupoItem.FamCod = &ItemProFamCod AND
                   &SdtSubGrupoItem.GrpCod = &ItemProGrpCod AND
                   &SdtSubGrupoItem.SubCod = &ItemProSubCod
                   &SdtSubGrupoItem.SubValor += &BolTitItem.TitVlrNom
                   &Existe = 1
                   Exit
                Endif
            EndFor

            If &Existe = 0
                &SdtSubGrupoItem = New()
                &SdtSubGrupoItem.FamCod     = &ItemProFamCod
                &SdtSubGrupoItem.GrpCod     = &ItemProGrpCod
                &SdtSubGrupoItem.SubCod     = &ItemProSubCod
                &SdtSubGrupoItem.SubNom     = &ItemProSubNom
                &SdtSubGrupoItem.SubQtdade  = 0
                &SdtSubGrupoItem.SubValor   = &BolTitItem.TitVlrNom
                &SdtSubGrupo.Add(&SdtSubGrupoItem)
            EndIf
        EndIf

        If &ImpVlrNom <> 1
            Print P_Boleto
        Else
            Print P_BoletoNominal
        Endif
        &TotGerTitVlrNom += &BolTitItem.TitVlrNom
        &TotVenTitNom    += &BolTitItem.TitVlrNom
        &TitChpCod  = &BolTitItem.TitChpCod
        &ContBol         += 1
        &ContBolVen      += 1
    EndIf
EndFor

If &Ordem in (6,7) and &TitVenCod > 0
    // Print Total
    Print P_Tot_Vendedor
    &ContBolVen     = 0
    &TotVenTitNom   = 0
EndIf

PRetParEmp.Call(&EmpParCod1)
Do Case
    Case &ContBol > 0 and Not &EmpParcod1 in(32,25)
        Print P_TotGer

    Case &ContBol > 0 and &EmpParcod1 in(32,25)
        Print P_TotGer1
EndCase

If &ImpResSubGrupo = 1 and &SdtSubGrupo.Count > 0
    &TotalSubGrupos = 0
    Print P_CabResSubGrupo
    &SdtSubGrupo.Sort('SubNom')
    For &SdtSubGrupoItem in &SdtSubGrupo
        &DesSubGrupo    = Trim(Str(&SdtSubGrupoItem.FamCod)) + ' | ' + Trim(Str(&SdtSubGrupoItem.GrpCod)) + ' | ' + Trim(Str(&SdtSubGrupoItem.SubCod)) + ' - ' + Trim(&SdtSubGrupoItem.SubNom)
        &TotSubGrupo    = &SdtSubGrupoItem.SubValor
        &TotalSubGrupos += &SdtSubGrupoItem.SubValor
        Print P_ResSubGrupo
    Endfor
    Print P_TotResSubGrupo
EndIf

NoSkip

&TitChpCod = 0
&TitVlrNomTot = 0
&TitChpDesHis =  ''
&TitVlrNom = 0
if &ImpResCHP = 1

    print P_ResChpCab
    &BolTit.Sort('TitChpCod,TitVlrNom')

    For &BolTitItem in &BolTit
        If &BolTitItem.TitChpCod <> &TitChpCod //and &TitChpCod <> 0
            if &TitVlrNomTot <> 0
                Print P_ResChpTot
            endif
            &TitVlrNom = 0
            &TotalRegistro = 0
        EndIf
        //If &BolTitItem.TitChpCod <> 0
            &TitChpCod = &BolTitItem.TitChpCod
            &TitChpDesHis  = &BolTitItem.TitChpDesHis
            &TitVlrNom += &BolTitItem.TitVlrNom
            &TitVlrNomTot  += &BolTitItem.TitVlrNom
            &TotalRegistro += 1
        //EndIf
            &Total += 1
    EndFor

    if &TitVlrNomTot <> 0
        Print P_ResChpTot

    EndIF

    Print P_ResChpTotais
EndIf

Footer
    print P_EstRelG
End

Sub 'LeTransacionador'
    For Each
        Where TraCod = &TitTraCodAux
        &TraNom = TraNom
    EndFor
EndSub

Sub 'LeConta'
    &ConSeq    = 0
    &ConConLev = ''
    &ConBanCod = 0
    &ConAgeCod = ''
    For each
        where ConCod    = &ConCod
        &ConSeq    = ConSeq
        &ConConLev = Trim(ConConLev)
        &ConBanCod = ConBanCod
        &ConAgeCod = ConAgeCod
    EndFor
EndSub

Sub 'LeNF'
    For each
        Where NfFilCod  = &NfFilCod
        Where NfDatEmis = &NfDatEmis
        Where NfNumero  = &NfNumero
        Where NfForCod  = &NfForCod
        Where ItemProCod > 0
        Defined by ItemQtdade
        &ItemProFamCod  = ItemProFamCod
        &ItemProGrpCod  = ItemProGrpCod
        &ItemProSubCod  = ItemProSubCod
        &ItemProSubNom  = ItemProSubNom
        Exit
        When None
            &ItemProFamCod  = 999999
            &ItemProGrpCod  = 999999
            &ItemProSubCod  = 999999
            &ItemProSubNom  = 'OUTROS'
    EndFor
EndSub